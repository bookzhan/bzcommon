apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.luoye.bzcommonlib'
}

ext {
    NEXUS_USER = ''
    NEXUS_PASSWORD = ''
    RELEASE_SNAPSHOT = true

    POM_URL_SNAP = 'https://www.raoyunsoft.com/nexus/repository/maven-snapshots/'
    POM_URL = 'https://www.raoyunsoft.com/nexus/repository/maven-releases/'

    POM_GROUP_ID = 'com.guaishou.bzlib'
    POM_ATRIFACT_ID = 'bzcommon'
    POM_VERSION = '1.1.39'
}
if (System.getenv('NEXUS_USER')) {
    NEXUS_USER = System.getenv('NEXUS_USER')
}
if (System.getenv('NEXUS_PASSWORD')) {
    NEXUS_PASSWORD = System.getenv('NEXUS_PASSWORD')
}
println("NEXUS_USER=" + NEXUS_USER + " NEXUS_PASSWORD=" + NEXUS_PASSWORD)

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = POM_GROUP_ID
                artifactId = POM_ATRIFACT_ID
                artifact androidSourcesJar
                version = RELEASE_SNAPSHOT ? POM_VERSION + "-SNAPSHOT" : POM_VERSION
            }
        }
        repositories {
            maven {
                url RELEASE_SNAPSHOT ? POM_URL_SNAP : POM_URL
                credentials {
                    username NEXUS_USER
                    password NEXUS_PASSWORD
                }
            }
        }
    }
}
tasks.withType(PublishToMavenRepository) { it.dependsOn lint }
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.getSrcDirs()
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}
